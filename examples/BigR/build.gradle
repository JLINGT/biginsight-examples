apply plugin: 'groovy'

// set the dependencies for running the groovy script
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.codehaus.groovy:groovy-all:2.0.5'
        classpath 'org.apache.ant:ant-jsch:1.9.4'
    }
}

// set the dependencies for compiling the groovy script
repositories {
    mavenCentral()
}

configurations {
    sshAntTask
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.0.5'
    sshAntTask 'org.apache.ant:ant-jsch:1.9.4'
}

// tell gradle the groovy script is in the same folder as the build.gradle file
sourceSets {
    main {
        groovy {
            srcDirs = ['.']
        }
    }
}

clean {
    delete './bigr.tar.gz'
    delete './lib'
}

// task to run a R script
task('Example', type: Exec) {

    Properties props = new Properties()
    props.load(new FileInputStream("$projectDir/../../connection.properties"))

    def matcher = props.gateway =~ /^(https?:\/\/)([^:^\/]*)(:\d*)?(.*)?.*$/
    def hostname = matcher[0][2] 

    if (!(new File("${projectDir}/../../certificate").exists())) {
        throw new GradleException("'certificate' file could not be found in ${projectDir.parentFile.parentFile}")
    }

    // import the BigInsights manager certificate
    ant.exec(executable: 'keytool', dir:'./') {
        arg(line: '-import -trustcacerts -alias biginsights -file ../../certificate -keystore ./truststore.jks -storepass mypassword -noprompt')
    }

    ant.taskdef(
            name: 'scp',
            classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
            classpath: configurations.sshAntTask.asPath
            )

    if (!(new File('./bigr.tar.gz').exists())) {

        // download BigR R package from the BigInsights cluster
        ant.scp(
            file: "${props.username}@${hostname}:/usr/ibmpacks/current/bigr/bigr/bigr_*.tar.gz",
            todir: "bigr.tar.gz",
            password: props.password
            )

    }

    // BigR service listens on master-2 node
    hostname = hostname.replace('mastermanager', 'master-2')

    // pass environment variables to the R script
    environment 'hostname', hostname
    environment 'gateway',  props.gateway
    environment 'username', props.username
    environment 'password', props.password

    workingDir './'

    // execute R script - inpect example.r for details
    commandLine 'Rscript', 'example.r'
}
