plugins {
  id 'org.hidetake.ssh' version '1.5.0'
}

Properties props = new Properties()
props.load(new FileInputStream("$projectDir/../../connection.properties"))

// extract BigInsights hostname from the gateway url
def matcher = props.gateway =~ /^(https?:\/\/)([^:^\/]*)(:\d*)?(.*)?.*$/
def hostname = matcher[0][2] 

// setup the connection details for ssh
remotes {
    bicluster {
       host = hostname
       user = props.username
       password = props.password
    }
}

ssh.settings {
    if (props.known_hosts == 'allowAnyHosts') {
        // disable ssh host key verification 
        knownHosts = allowAnyHosts
    }
}

task Example << {

    def tmpDir = "test-${new Date().getTime()}"

    def tmpHdfsDir = "/user/${props.username}/${tmpDir}"
    
    // ssh plugin documentation: https://gradle-ssh-plugin.github.io/docs/
    
    ssh.run {
        session(remotes.bicluster) {

            try {
                // initialise kerberos
                execute "kinit -k -t ${props.username}.keytab ${props.username}@IBM.COM"
            } 
            catch (Exception e) {
                println "problem running kinit - maybe this is a Basic cluster?"
            }

            // create temp local dir for sparkstreaming.py script
            execute "mkdir ${tmpDir}"

            // upload spark script
            put from: "${projectDir}/sparkstreaming.py", into: "${tmpDir}/sparkstreaming.py"
            put from: "${projectDir}/wordsocket.py", into: "${tmpDir}/wordsocket.py"

            executeBackground("python ${tmpDir}/wordsocket.py")

            // execute spark job
            execute "pyspark ${tmpDir}/sparkstreaming.py"

            // remove temporary local dir
            execute "rm -rf ${tmpDir}"

        }
    }
}
