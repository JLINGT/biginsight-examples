plugins {
  id 'groovy'
  id 'org.hidetake.ssh' version '1.5.0'
}

// get the cluster connection details
Properties props = new Properties()
props.load(new FileInputStream("$projectDir/../../connection.properties"))

// extract BigInsights hostname from the gateway url
def matcher = props.gateway =~ /^(https?:\/\/)([^:^\/]*)(:\d*)?(.*)?.*$/
def hostname = matcher[0][2] 

// setup the connection details for ssh
remotes {
    bicluster {
       host = hostname
       user = props.username
       password = props.password
    }
}

clean {
    delete './bigr.tar.gz'
    delete './lib'
}

task ('installPackages', type:Exec) {

    // ensure the ssl certificate exists
    if (!(new File("${projectDir}/../../certificate").exists())) {
        throw new GradleException("'certificate' file could not be found in ${projectDir.parentFile.parentFile}")
    }

    // import the BigInsights manager certificate
    ant.exec(executable: 'keytool', dir:'./') {
        arg(line: '-import -trustcacerts -alias biginsights -file ../../certificate -keystore ./truststore.jks -storepass mypassword -noprompt')
    }

    // only grab the file if it doesn't already exist locally
    if (!(new File('./bigr.tar.gz').exists())) {

        ssh.run {
            session(remotes.bicluster) {

                // grab the bigr package from the cluster
                get from: "/usr/ibmpacks/current/bigr/bigr/bigr_*.tar.gz",
                    into: "${projectDir}/bigr.tar.gz"
            }
        }
    }

    commandLine 'Rscript', 'install_packages.r'
}

// task to run a R script
task('Example', dependsOn: installPackages, type: Exec) {


    // BigR service listens on master-2 node
    hostname = hostname.replace('mastermanager', 'master-2')

    // pass environment variables to the R script
    environment 'hostname', hostname
    environment 'gateway',  props.gateway
    environment 'username', props.username
    environment 'password', props.password

    // execute R script - inpect example.r for details
    commandLine 'Rscript', 'example.r'
}
